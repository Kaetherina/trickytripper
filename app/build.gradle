apply plugin: 'com.android.application'

android {
    compileSdkVersion 21
    buildToolsVersion '21.1.2'
    defaultConfig {
        applicationId 'de.koelle.christian.trickytripper'
        minSdkVersion 8
        targetSdkVersion 21
        versionCode 14
        versionName '1.5.1'
        // ------------> This used to be required for for com.github.jcandksolutions.gradle
        //packageName "de.koelle.christian.trickytripper"
    }
    File signFile = rootProject.file('sign/keystore.properties')
    if (signFile.exists()) {
        Properties properties = new Properties()
        properties.load(new FileInputStream(signFile))
        signingConfigs {
            release {
                storeFile rootProject.file(properties['keystore'])
                storePassword properties['storePassword']
                keyAlias properties['keyAlias']
                keyPassword properties['keyPassword']
            }
        }
    }

    buildTypes {
        release {
            // This used to work before going to 1.0.0. RC of new build system.
            //runProguard true
            //zipAlign true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    productFlavors {
    }
}

// ------------> This needs to be after the android block
apply plugin: 'android-unit-test'

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.android.support:appcompat-v7:$comAndroidSupport_supportV7Version"
    compile "com.android.support:support-v4:$comAndroidSupport_supportV4Version"
    //compile 'com.loopj.android:android-async-http:1.4.2'  //--> Has api changes
    testCompile "junit:junit:$junitVersion"
    testCompile "org.robolectric:robolectric:$orgRobolectricRobolectricVersion"
    testCompile "org.mockito:mockito-core:$orgMockitoMockitoCoreVersion"
}
